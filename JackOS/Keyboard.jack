// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static char NEW_LINE, BACK_SPACE;

    /** Initializes the keyboard. */
    function void init() {
        let NEW_LINE = String.newLine();
        let BACK_SPACE = String.backSpace();
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char key;

        // Wait until key pressed
        let key = 0;
        while(key = 0){
            let key = Keyboard.keyPressed();
        }
        // Wait until key released
        while(true){
            if(Keyboard.keyPressed()=0){
                if(key < NEW_LINE){
                    // only show printable char
                    do Output.printChar(key);
                }
                return key;
            }
        }
        return key;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;
        let s = String.new(64); // Enough length for chars for one line.
        do Output.printString(message);
        while(true){
            let c = Keyboard.readChar();
            if(c = NEW_LINE){
                do Output.println();
                return s;
            }
            if(c = BACK_SPACE){
                if(s.length() > 0){
                    do s.eraseLastChar();
                    do Output.backSpace();
                }
            }else{
                if(c < NEW_LINE){
                    // only add printable char
                    do s.appendChar(c);
                }
            }
        }
        return s;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String line;
        var int result;
        let line = Keyboard.readLine(message);
        let result = line.intValue();
        do line.dispose();
        return result;
    }
}
